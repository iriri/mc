use sys
use std

use "tls"

pkg thread =
	const spawn    : (fn : (-> void) -> std.result(tid, byte[:]))
;;

const Stacksz = 8*std.MiB
extern const exit : (-> void)

const __init__ = {
	tlsmain = (std.bytealloc(sizeof(hdr) + (8 * sizeof(void#))) : hdr#)
	sys.arch_prctl(sys.Archsetfs, (tlsmain : void#))
	tlsmain.tid = 0
}

/* Holy shit flag mania. */
const Thrflag = sys.Clonevm | sys.Clonefs | sys.Clonefiles  | \
	sys.Clonesighand | sys.Clonethread |sys.Clonesysvsem | \
	sys.Clonesettls | sys.Cloneparentsettid | sys.Clonechildcleartid

const spawn = {fn
	-> spawnstk(fn, Stacksz)
}

const spawnstk = {fn, sz
	var stk, tos, hdr, tid, ctid, ret

	/* TODO: is there a pagesz const somewhere? */
	sz = (sz + 0xfff) & ~0xfff
	stk = getstk(sz)
	if stk == sys.Mapbad
		-> `std.Err "couldn't get stack"
	;;
	(tos, fn, hdr) = initstk(stk, fn, sz)

	ret = sys.fnclone(Thrflag, \
		tos,\
		&tid, (0 : byte#), \
		&ctid, (0 : byte#), \
		(startthread : void#))
	if ret < 0
		/* XXX: leaks? */
		-> `std.Err "couldn't spawn thread"
	;;
	-> `std.Ok (ret : tid)
}

const initstk = {stk, fn, sz
	var tos, hdr, fp, env, envsz

	tos = (stk : std.intptr) + (sz : std.intptr) - ((tlscap * sizeof(void#)) : std.intptr)
	hdr = ((tos -= sizeof(hdr)) : hdr#)
	hdr.base = stk

	var fn1 = {
		sys.arch_prctl(sys.Archsetfs, (hdr : void#))
		fn()
	}

	envsz = std.fnenvsz(fn1)
	tos -= (envsz : std.intptr)
	env = tos
	tos -= sizeof((->void))
	fp = (tos : (->void)#)
	fp# = std.fnbdup(fn1, (env : byte#)[:envsz])
	-> ((tos : byte#), fn1, hdr)
}

const getstk = {sz
	-> sys.mmap((0 : byte#), sz, sys.Mprotrw, sys.Mpriv | sys.Manon, -1, 0)
}

const startthread = {fn : (-> void)
	fn()
	exit()
}

