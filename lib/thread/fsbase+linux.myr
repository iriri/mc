use std
use sys

use "common"

pkg thread =
	/*
	the offset of `slots` must be a multiple of 16 so we use 32 bit values
	for `stksz` and `nslots` as neither actually needs 64 bits
	*/
	pkglocal type tlshdr = struct
		tid   : tid /* 32 bits */
		pad   : uint32
		stksz : uint32
		len   : uint32
		slots : void#[...]
	;;

	pkglocal const setfsbase : (h : tlshdr# -> void)
	pkglocal const getfsbase : (-> tlshdr#)
;;

const setfsbase = {h
	match sys.arch_prctl(sys.Archsetfs, (h : void#))
	| 0:
	| err:
		std.fput(std.Err, "error: arch_prctl returned: {}\n", err)
		std.suicide()
	;;
}

const getfsbase = {
	var h : tlshdr#
	match sys.arch_prctl(sys.Archgetfs, (&h : void#))
	| 0: -> h
	| err:
		std.fput(std.Err, "error: arch_prctl returned: {}\n", err)
		std.suicide()
	;;
}
