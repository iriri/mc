use std
use sys

use "tls"

pkg thread =
	const spawn : (fn : (-> void) -> std.result(tid, byte[:]))
	pkglocal var exitstk : byte#
;;

const Stacksz = 8*std.MiB
extern const exit : (-> void)

var exitstk
const __init__ = {
	/* 
	  We need a valid stack at all times, so we allocate one ahead of
	  time to swap to before we invalidate a stack.
	 */
	exitstk = getstk(16)
	std.assert(exitstk != sys.Mapbad, "error: failed to mmap exitstk\n")
}

const spawn = {fn;
	-> spawnstk(fn, Stacksz)
}

const spawnstk = {fn, sz
	var stk, tos, hdr, tfp, ret

	spawned = true
	stk = getstk(sz)
	if stk == sys.Mapbad
		-> `std.Err "couldn't get stack"
	;;
	(tos, hdr) = initstk(stk, fn, sz)

	tfp = [
		.tcb = (hdr : void#),
		.tid = (&hdr.tid : sys.pid#), /* TODO: writes 64, we only care about 32(?), fix this. */
		.stk = (tos : byte#),
	]
	ret = sys.__tfork_thread(&tfp,
		sizeof(sys.tforkparams),
		(startthread : void#),
		(0 : void#))
	if ret < 0
		-> `std.Err "couldn't spawn thread"
	;;
	-> `std.Ok (ret : tid)
}

const initstk = {stk, fn, sz
	var tos, hdr, fp, env, envsz

	tos = (stk : std.intptr) + (sz : std.intptr)
	tos -= sizeof(hdr) + ((tlscap * sizeof(void#)) : std.intptr)
	hdr = (tos : hdr#)
	hdr.base = stk

	envsz = std.fnenvsz(fn)
	tos -= (envsz : std.intptr)
	env = tos
	tos -= sizeof((->void))
	fp = (tos : (->void)#)
	fp# = std.fnbdup(fn, (env : byte#)[:envsz])
	-> ((tos : byte#), hdr)
}

const getstk = {sz
	-> sys.mmap((0 : byte#), sz, sys.Mprotrw, sys.Mpriv | sys.Manon, -1, 0)
}

const startthread = {fn : (-> void)
	fn()
	exit()
}
